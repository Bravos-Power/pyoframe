from shutil import move
import gdown
from urllib.request import urlretrieve
from pathlib import Path

CATS_GITHUB_URL = "https://raw.githubusercontent.com/WISPO-POP/CATS-CaliforniaTestSystem/f260d8bd89e68997bf12d24e767475b2f2b88a77/GIS/"

ENERGY_BENCHMARKS = Path("input_data/energy_planning")
PREPROCESS_DIR = ENERGY_BENCHMARKS / "intermediary_data_inputs"
POSTPROCESS_DIR = ENERGY_BENCHMARKS
SCRIPTS_DIR = Path("./scripts")

rule generate_all_inputs:
    input:
        loads=POSTPROCESS_DIR / "loads.parquet",
        lines=POSTPROCESS_DIR / "lines_simplified.parquet",
        generators=POSTPROCESS_DIR / "generators.parquet",
        yearly_limit=POSTPROCESS_DIR / "yearly_limits.parquet",
        vcf=POSTPROCESS_DIR / "variable_capacity_factors.parquet",
        bodf=POSTPROCESS_DIR / "branch_outage_dist_facts.parquet",
    output:
        POSTPROCESS_DIR / "COMPLETED"
    shell:
        "touch {output[0]}"


rule fetch_load_data:
    """Downloads the load data from the Google Drive folder hosted by the CATS project (https://drive.google.com/drive/folders/1Zo6ZeZ1OSjHCOWZybbTd6PgO4DQFs8_K)"""
    output:
        PREPROCESS_DIR / "CATS_loads.csv",
    run:
        gdown.download(id="1Sz8st7g4Us6oijy1UYMPUvkA1XeZlIr8", output=output[0])


rule fetch_generation_data:
    """Downloads the generation data from the Google Drive folder hosted by the CATS project (https://drive.google.com/drive/folders/1Zo6ZeZ1OSjHCOWZybbTd6PgO4DQFs8_K)"""
    output:
        PREPROCESS_DIR / "CATS_generation.csv",
    run:
        gdown.download(id="1CxLlcwAEUy-JvJQdAfVydJ1p9Ecot-4d", output=output[0])


rule fetch_line_data:
    output:
        PREPROCESS_DIR / "CATS_lines.json",
    run:
        urlretrieve(CATS_GITHUB_URL + "CATS_lines.json", output[0])


rule fetch_generator_data:
    output:
        PREPROCESS_DIR / "CATS_generators.csv",
    run:
        urlretrieve(CATS_GITHUB_URL + "CATS_gens.csv", output[0])


rule process_load_data:
    """Convert the load data to narrow format and keep only the active loads."""
    input:
        PREPROCESS_DIR / "CATS_loads.csv",
    output:
        POSTPROCESS_DIR / "loads.parquet",
    notebook:
        str(SCRIPTS_DIR / "process_load_data.py.ipynb")


rule process_line_data:
    """Convert from .json to .parquet and keep only relevant columns."""
    input:
        PREPROCESS_DIR / "CATS_lines.json",
    output:
        PREPROCESS_DIR / "lines.parquet",
    notebook:
        str(SCRIPTS_DIR / "process_lines_json.py.ipynb")


rule process_generator_data:
    """Group the generators by type and bus."""
    input:
        PREPROCESS_DIR / "CATS_generators.csv",
    output:
        POSTPROCESS_DIR / "generators.parquet",
    notebook:
        str(SCRIPTS_DIR / "process_generator_data.py.ipynb")


rule compute_capacity_factors:
    """Use the hourly generation data to create capacity factors by fuel type."""
    input:
        gen_capacity=POSTPROCESS_DIR / "generators.parquet",
        gen_dispatch=PREPROCESS_DIR / "CATS_generation.csv",
    output:
        yearly_limit=POSTPROCESS_DIR / "yearly_limits.parquet",
        vcf=POSTPROCESS_DIR / "variable_capacity_factors.parquet",
    notebook:
        str(SCRIPTS_DIR / "compute_capacity_factors.py.ipynb")


rule simplify_network:
    input:
        lines=PREPROCESS_DIR / "lines.parquet",
        generators=POSTPROCESS_DIR / "generators.parquet",
        loads=POSTPROCESS_DIR / "loads.parquet",
    output:
        POSTPROCESS_DIR / "lines_simplified.parquet",
    script:
        SCRIPTS_DIR / "simplify_network.py"

rule compute_ptdf:
    input:
        lines=POSTPROCESS_DIR / "lines_simplified.parquet",
    output:
        PREPROCESS_DIR / "power_transfer_dist_facts.parquet",
    notebook:
        str(SCRIPTS_DIR / "compute_power_transfer_dist_facts.ipynb")

rule compute_bodf:
    input:
        lines=POSTPROCESS_DIR / "lines_simplified.parquet",
        ptdf=PREPROCESS_DIR / "power_transfer_dist_facts.parquet",
    output:
        POSTPROCESS_DIR / "branch_outage_dist_facts.parquet",
    notebook:
        str(SCRIPTS_DIR / "compute_branch_outage_dist_facts.ipynb")
